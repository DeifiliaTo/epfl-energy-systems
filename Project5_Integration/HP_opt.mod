/*---------------------------------------------------------------------------------------------------------------------------------------
Sets
---------------------------------------------------------------------------------------------------------------------------------------*/
set Time default {}; 											# time segments of the problem

/*---------------------------------------------------------------------------------------------------------------------------------------
Parameters
---------------------------------------------------------------------------------------------------------------------------------------*/

param Text{Time};
param Qheatingsupply := 1000;
param T_evap := 5.5;
param T_cond := 65;

/*---------------------------------------------------------------------------------------------------------------------------------------
Carnot factors
---------------------------------------------------------------------------------------------------------------------------------------*/

param a_HP = 0.00270757;
param b_HP = -1.4653;
param c_HP = 198.516;

param a_LP = 0.00105432;
param b_LP = -0.569855;
param c_LP = 77.4214;

/*---------------------------------------------------------------------------------------------------------------------------------------
Variables
---------------------------------------------------------------------------------------------------------------------------------------*/
var Q_evap{Time} >= 0.001;
var T_sep >= 0.001;
var FlowInUnit >= 0.001;

var f_HP{Time} >= 0.001;
var f_LP{Time} >= 0.001;

var E_HP{Time} >= 0.001;
var E_LP{Time} >= 0.001;

subject to Tsepmin:
    T_sep - T_evap >= 0;

subject to Tsepmax:
    T_cond - T_sep >= 0;

/*---------------------------------------------------------------------------------------------------------------------------------------
Equations
---------------------------------------------------------------------------------------------------------------------------------------*/

subject to f1 {t in Time}:
    f_HP[t] = a_HP * (Text[t] + 273)^2 + b_HP * (Text[t] + 273) + c_HP;

subject to f2 {t in Time}:
    f_LP[t] = a_LP * (Text[t] + 273)^2 + b_LP * (Text[t] + 273) + c_LP;

subject to COP_HP {t in Time}:
    Qheatingsupply * (T_cond - T_sep) = f_HP[t] * (T_cond + 273) * E_HP[t];

subject to COP_LP {t in Time}:
    (Qheatingsupply - E_HP[t]) * (T_sep - T_evap) = f_LP[t] * (T_sep + 273) * E_LP[t];

subject to EnergyBalance {t in Time}:
    Qheatingsupply = Q_evap[t] + E_HP[t] + E_LP[t];

subject to Elec:
    FlowInUnit = sum{t in Time} (E_HP[t] + E_LP[t]);

/*---------------------------------------------------------------------------------------------------------------------------------------
Objective function
---------------------------------------------------------------------------------------------------------------------------------------*/

minimize obj : FlowInUnit;
